/*
 * Copyright (C) 2016 - present Instructure, Inc.
 *
 *     Licensed under the Apache License, Version 2.0 (the "License");
 *     you may not use this file except in compliance with the License.
 *     You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 *     Unless required by applicable law or agreed to in writing, software
 *     distributed under the License is distributed on an "AS IS" BASIS,
 *     WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *     See the License for the specific language governing permissions and
 *     limitations under the License.
 *
 */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.devtools.ksp'

static String isTesting() {
    if ( System.getenv("IS_TESTING") == "true" ) {
        "true"
    } else {
        "false"
    }
}

android {
    namespace 'com.instructure.annotations'
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    compileSdkVersion Versions.COMPILE_SDK

    defaultConfig {
        minSdkVersion Versions.MIN_SDK
        targetSdkVersion Versions.TARGET_SDK

        /* Add private data */
        PrivateData.merge(project, "annotations")

        /* BuildConfig Fields */
        buildConfigField "boolean", "IS_TESTING", isTesting()
    }

    buildTypes {
        debug{
            debuggable true
            minifyEnabled false
            shrinkResources false
            buildConfigField 'boolean', 'IS_DEBUG', 'true'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release{
            debuggable false
            minifyEnabled false
            shrinkResources false
            buildConfigField 'boolean', 'IS_DEBUG', 'false'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packaging {
        resources {
            excludes += [
                'META-INF/DEPENDENCIES',
                'META-INF/LICENSE',
                'META-INF/LICENSE.txt',
                'META-INF/NOTICE',
                'LICENSE.txt'
            ]
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    configurations.all {
        resolutionStrategy.force 'com.getkeepsafe.relinker:relinker:1.4.3'
    }
}


repositories {
    google()
    mavenCentral()
    maven {
        credentials {
            username pspdfMavenUser
            password pspdfMavenPass
        }
        url 'https://my.nutrient.io/maven'
    }
}

dependencies {
    /* Modules */
    api project(path: ':pandautils')
    api project(path: ':pandares')

    api Libs.NUTRIENT

    androidTestImplementation Libs.JUNIT
    testImplementation Libs.JUNIT

    /* Support Dependencies */
    implementation Libs.ANDROIDX_ANNOTATION
    implementation Libs.ANDROIDX_APPCOMPAT
    implementation Libs.MATERIAL_DESIGN
    implementation Libs.ANDROIDX_FRAGMENT
    implementation Libs.ANDROIDX_RECYCLERVIEW

    // This dependency is needed for PSPDFKit, because they use the 1.4.1 version, which cannot be found on Maven Central
    implementation 'com.getkeepsafe.relinker:relinker:1.4.3'
}
