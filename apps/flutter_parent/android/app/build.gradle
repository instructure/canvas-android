def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdk 33

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    defaultConfig {
        applicationId "com.instructure.parentapp"
        minSdkVersion 26
        targetSdk 33
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        //noinspection GroovyAccessibility
        PrivateData.merge(project, "parent")
        PrivateData.merge(project, "dataseeding-flutter")
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }

            minifyEnabled true
            shrinkResources false // Must be false, otherwise resources we need are erroneously stripped out
            proguardFiles 'proguard-rules.pro'
        }
        applicationVariants.all{
            variant ->
                variant.outputs.each{
                    output->
                        project.ext { appName = 'parent' }
                        def dateTimeStamp = new Date().format('yyyy-MM-dd-HH-mm-ss')
                        def newName = output.outputFile.name
                        newName = newName.replace("app-", "$project.ext.appName-")
                        newName = newName.replace("-debug", "-dev-debug-" + dateTimeStamp)
                        newName = newName.replace("-release", "-prod-release")
                        output.outputFileName  = newName
                }
        }
    }
    namespace 'com.instructure.parentapp'
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.5.20"

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation "com.squareup.okhttp3:okhttp:4.9.1"
    implementation 'org.jsoup:jsoup:1.11.3'
    implementation 'com.google.gms:google-services:4.3.14'
}

apply plugin: 'com.google.firebase.crashlytics'

// Comment out this line if the reporting logic starts going wonky.
gradle.addListener new TimingsListener(project)