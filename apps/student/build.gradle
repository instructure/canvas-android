/*
 * Copyright (C) 2016 - present Instructure, Inc.
 *
 *     This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU General Public License as published by
 *     the Free Software Foundation, version 3 of the License.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU General Public License for more details.
 *
 *     You should have received a copy of the GNU General Public License
 *     along with this program.  If not, see <http://www.gnu.org/licenses/>.
 *
 */

import com.instructure.android.buildtools.transform.*

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'
apply from: '../../gradle/coverage.gradle'
apply plugin: 'com.squareup.sqldelight'
apply plugin: 'dagger.hilt.android.plugin'

def updatePriority = 2
def coverageEnabled = project.hasProperty('coverage')

if (coverageEnabled) {
    apply from: '../../gradle/jacoco.gradle'
}

sqldelight {
    StudentDb {
        packageName = "com.instructure.student.db"
        sourceFolders = ["db"]
        schemaOutputDirectory = file("src/main/db/schemas")
    }
}


configurations {
    all*.exclude group: 'commons-logging', module: 'commons-logging'
    all*.exclude group: 'org.apache.httpcomponents', module: 'httpclient'
}

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion Versions.COMPILE_SDK
    buildToolsVersion Versions.BUILD_TOOLS

    defaultConfig {
        applicationId "com.instructure.candroid"
        minSdkVersion Versions.MIN_SDK
        targetSdkVersion Versions.TARGET_SDK
        versionCode = 236
        versionName = '6.16.2'

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled = true
        testInstrumentationRunner 'com.instructure.student.espresso.StudentHiltTestRunner'
        testInstrumentationRunnerArguments disableAnalytics: 'true'
        buildConfigField "boolean", "IS_TESTING", "false"

        /* Add private data */
        PrivateData.merge(project, "student")
        buildConfigField "String", "PSPDFKIT_LICENSE_KEY", "\"$pspdfkitLicenseKey\""

        testBuildType = "debug"
    }

    bundle {
        language {
            // Specifies that the app bundle should not support configuration APKs for language resources. These
            // resources are instead packaged with each base and dynamic feature APK.
            enableSplit = false
        }
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/rxjava.properties'
        exclude 'LICENSE.txt'
    }

    lintOptions {
        abortOnError false
        // Work around for AGP issue related to release mode, see:
        // https://github.com/flutter/flutter/issues/58247#issuecomment-636253593
        checkReleaseBuilds false
    }

    signingConfigs {
        release {
            storeFile file('../candroid.keystore')
            storePassword System.getenv('KEYSTORE_PASSWORD')
            keyAlias System.getenv('KEY_ALIAS')
            keyPassword System.getenv('KEY_PASSWORD')
        }
    }

    buildTypes {

        debug {
            testCoverageEnabled = coverageEnabled
            debuggable true
            minifyEnabled false
            shrinkResources false
            buildConfigField 'boolean', 'IS_DEBUG', 'true'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            pseudoLocalesEnabled true

            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }

            ndk {
                // Filter for architectures supported by Flutter. Include x86 in debug builds so we don't crash on FTL.
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64', 'x86'
            }
        }

        release {
            signingConfig signingConfigs.release
            debuggable false
            minifyEnabled true
            shrinkResources true
            buildConfigField 'boolean', 'IS_DEBUG', 'false'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'

            firebaseCrashlytics {
                nativeSymbolUploadEnabled true
            }

            ndk {
                // Filter for architectures supported by Flutter. Exclude x86 in release builds to avoid missing lib crash on x86 devices.
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64'
            }
        }
    }

    flavorDimensions 'default'

    productFlavors {
        dev {
            dimension 'default'
        }

        qa {
            buildConfigField "boolean", "IS_TESTING", "true"
            dimension 'default'
        }

        prod {
            dimension 'default'
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    testOptions.unitTests.includeAndroidResources = true
    testOptions.animationsDisabled = true

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
        /*
        Resolves dependency versions across test and production APKs, specifically, transitive
        dependencies. This is required since Espresso internally has a dependency on support-annotations.
        https://github.com/googlecodelabs/android-testing/blob/57852eaf7df88ddaf828eca879a407f2249d5348/app/build.gradle#L86
        */
        resolutionStrategy.force Libs.ANDROIDX_ANNOTATION

        resolutionStrategy.force Libs.KOTLIN_COROUTINES_CORE
    }

    /*
    All direct/transitive dependencies shared between your test and production APKs need to be
    excluded from the test APK! This is necessary because both APKs will contain the same classes. Not
    excluding these dependencies from your test configuration will result in an dex pre-verifier error
    at runtime. More info in this tools bug: (https://code.google.com/p/android/issues/detail?id=192497)
    */
    configurations.implementation.dependencies.each { compileDependency ->
        println "Excluding compile dependency: ${compileDependency.getName()}"
        configurations.androidTestImplementation.dependencies.each { androidTestCompileDependency ->
            configurations.androidTestImplementation.exclude module: "${compileDependency.getName()}"
        }
    }

    dexOptions {
        javaMaxHeapSize '4g'
    }

    registerTransform(
            new ProjectTransformer(
                    android,
                    new MasqueradeUITransformer('com.instructure.student.activity.NavigationActivity.class'),
                    new PageViewTransformer(),
                    new ScreenViewTransformer("student"),
                    new LocaleTransformer(project),
                    new FlutterA11yOffsetTransformer(),
                    new FlutterTextureDisconnectFix()
            )
    )

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        dataBinding true
    }

    hilt {
        enableTransformForLocalTests = true
    }
}

dependencies {
    implementation project(path: ':panda_annotations')
    kaptAndroidTestQa project(path: ':panda_processor')

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    /* Project Modules */
    implementation project(path: ':login-api-2')
    implementation project(path: ':annotations')
    implementation project(path: ':rceditor')
    implementation project(path: ':interactions')
    implementation project(path: ':DocumentScanner')

    /* Flutter embed */
    implementation (project(path: ':flutter-student-embed')) {
        exclude group: 'com.google.firebase'
        exclude group: 'com.google.android.gms'
    }

    /* Android Test Dependencies */
    androidTestImplementation project(path: ':espresso')
    androidTestImplementation project(':dataseedingapi')

    /* Unit Test Dependencies */
    testImplementation Libs.JUNIT
    testImplementation Libs.ROBOLECTRIC
    testImplementation Libs.ANDROIDX_TEST_JUNIT
    testImplementation Libs.MOCKK
    androidTestImplementation Libs.ANDROIDX_TEST_JUNIT
    testImplementation Libs.KOTLIN_COROUTINES_TEST
    testImplementation Libs.FIREBASE_CORE
    testImplementation Libs.THREETEN_BP
    testImplementation Libs.ANDROIDX_CORE_TESTING

    implementation (Libs.FIREBASE_ANALYTICS) {
        transitive = true
    }

    implementation (Libs.FIREBASE_CRASHLYTICS) {
        transitive = true
    }

    /* Kotlin */
    implementation Libs.KOTLIN_STD_LIB

    /* Mobius */
    implementation Libs.MOBIUS_CORE
    testImplementation Libs.MOBIUS_TEST
    implementation Libs.MOBIUS_ANDROID
    implementation Libs.MOBIUS_EXTRAS

    /* Media Handling */
    implementation Libs.PHOTO_VIEW
    implementation Libs.LOTTIE

    /* Sliding Panel */
    implementation Libs.SLIDING_UP_PANEL

    /* Apache Commons */
    implementation Libs.APACHE_COMMONS_TEXT

    /* Support dependencies */
    implementation Libs.ANDROIDX_ANNOTATION
    implementation Libs.ANDROIDX_APPCOMPAT
    implementation Libs.ANDROIDX_BROWSER
    implementation Libs.ANDROIDX_CARDVIEW
    implementation Libs.ANDROIDX_CONSTRAINT_LAYOUT
    implementation Libs.ANDROIDX_DESIGN
    implementation Libs.ANDROIDX_RECYCLERVIEW
    implementation Libs.PLAY_SERVICES_ANALYTICS
    implementation Libs.FIREBASE_MESSAGING
    implementation Libs.ANDROIDX_PALETTE
    implementation Libs.PLAY_CORE

    /* Database */
    implementation Libs.SQLDELIGHT

    /* Qr Code */
    implementation Libs.JOURNEY_ZXING

    /* AAC */
    implementation Libs.VIEW_MODEL
    implementation Libs.LIVE_DATA
    implementation Libs.VIEW_MODE_SAVED_STATE
    implementation Libs.FRAGMENT_KTX
    kapt Libs.LIFECYCLE_COMPILER

    /* DI */
    implementation Libs.HILT
    kapt Libs.HILT_COMPILER
    androidTestImplementation Libs.HILT_TESTING
    kaptAndroidTestQa Libs.HILT_TESTING_COMPILER

    androidTestImplementation Libs.UI_AUTOMATOR

    implementation Libs.FIREBASE_CRASHLYTICS_NDK
}

// Comment out this line if the reporting logic starts going wonky.
gradle.addListener new TimingsListener(project)

apply plugin: 'com.google.gms.google-services'

if (coverageEnabled) {
    // ./gradlew -Pcoverage firebaseJacoco
    task firebaseJacoco(type: JacocoReport) {
        group = "Reporting"
        description = "Generate Jacoco coverage reports for Firebase test lab."

        def productFlavor = 'qa'
        def buildType = 'debug'

        classDirectories.setFrom fileTree(
                dir: "${project.buildDir}/tmp/kotlin-classes/qaDebug",
                excludes: ['**/R.class',
                           '**/R$*.class',
                           '**/*$ViewInjector*.*',
                           '**/*$ViewBinder*.*',
                           '**/BuildConfig.*',
                           '**/Manifest*.*'],
                //includes: ['**/com/instructure/*']
        )

        // project.buildDir is /canvas-android/apps/teacher/build
        sourceDirectories.setFrom files(['src/main/java'] + android.sourceSets[productFlavor].java.srcDirs)
        executionData.setFrom fileTree(dir: "results", includes: ['**/*.ec'])

        reports {
            // default path: /canvas-android/apps/teacher/build/reports/jacoco/firebaseJacoco/html/
            html.enabled true
            csv.enabled  false
            xml.enabled  false
        }
    }

    task combinedJacoco(type: JacocoReport) {
        group = "Reporting"
        description = "Generate Jacoco coverage reports for FTL + unit tests."

        def productFlavor = 'qa'
        def buildType = 'debug'

        classDirectories.setFrom fileTree(
                dir: "${project.buildDir}/tmp/kotlin-classes/qaDebug",
                excludes: ['**/R.class',
                           '**/R$*.class',
                           '**/*$ViewInjector*.*',
                           '**/*$ViewBinder*.*',
                           '**/BuildConfig.*',
                           '**/Manifest*.*'],
                //includes: ['**/com/instructure/*']
        )

        // project.buildDir is /canvas-android/apps/teacher/build
        sourceDirectories.setFrom files(['src/main/java'] + android.sourceSets[productFlavor].java.srcDirs)
        executionData.setFrom fileTree(dir: ".", includes: ['results/**/*.ec', '**/testQaDebugUnitTest.exec'])

        reports {
            // default path: /canvas-android/apps/teacher/build/reports/jacoco/firebaseJacoco/html/
            html.enabled true
            csv.enabled  false
            xml.enabled  false
        }
    }

}
