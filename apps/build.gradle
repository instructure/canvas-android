/*
 * Copyright (C) 2017 - present Instructure, Inc.
 *
 *     Licensed under the Apache License, Version 2.0 (the "License");
 *     you may not use this file except in compliance with the License.
 *     You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 *     Unless required by applicable law or agreed to in writing, software
 *     distributed under the License is distributed on an "AS IS" BASIS,
 *     WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *     See the License for the specific language governing permissions and
 *     limitations under the License.
 *
 */

buildscript {
    PrivateData.merge(project, "all-apps")
    project.ext.setProperty("coverageEnabled", project.hasProperty("testCoverageEnabled"))

    repositories {
        google()
        mavenCentral()
        maven { url "https://www.jitpack.io" }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath Plugins.ANDROID_GRADLE_TOOLS
        classpath Plugins.APOLLO_3
        classpath Plugins.GOOGLE_SERVICES
        classpath Plugins.KOTLIN
        classpath Plugins.FIREBASE_CRASHLYTICS
        if (project.coverageEnabled) { classpath Plugins.JACOCO_ANDROID }
        classpath Plugins.HILT
        classpath Plugins.HEAP
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven {
            credentials {
                username pspdfMavenUser
                password pspdfMavenPass
            }
            url 'https://customers.pspdfkit.com/maven/'
        }
        maven { url "https://maven.google.com/" }
    }
}

task assembleAllApps() {
    dependsOn ':student:assembleDevDebug'
    dependsOn ':teacher:assembleDevDebug'
}

apply from: '../gradle/jacoco.gradle'

configurations.all{
    resolutionStrategy {
        eachDependency { details ->
            if ('org.jacoco' == details.requested.group) {
                details.useVersion "0.8.7"
            }
        }
    }
}

