/*
 * Copyright (C) 2017 - present Instructure, Inc.
 *
 *     Licensed under the Apache License, Version 2.0 (the "License");
 *     you may not use this file except in compliance with the License.
 *     You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 *     Unless required by applicable law or agreed to in writing, software
 *     distributed under the License is distributed on an "AS IS" BASIS,
 *     WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *     See the License for the specific language governing permissions and
 *     limitations under the License.
 */

buildscript {
    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.2'
        classpath Plugins.KOTLIN
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'digital.wup:android-maven-publish:3.6.2'
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

android {
    namespace 'com.instructure.espresso'
    compileSdkVersion Versions.COMPILE_SDK
    buildToolsVersion Versions.BUILD_TOOLS

    defaultConfig {
        minSdkVersion Versions.MIN_SDK
        targetSdkVersion Versions.TARGET_SDK
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
        }
        release {
            minifyEnabled false
            shrinkResources false
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        // Required for Espresso.
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }

    // Android Studio doesn't pick up kotlin folder automatically
    // https://kotlinlang.org/docs/reference/using-gradle.html
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {

    implementation project(':dataseedingapi')
    implementation project(':login-api-2')
    androidTestImplementation Libs.COMPOSE_UI_TEST

    implementation project(':pandautils')

    // last update: Sept 30 2017
    // old versions: $ANDROID_HOME/extras/android/m2repository/com/android/support/test/
    // new versions: https://maven.google.com
    //
    // match versions from:
    // https://developer.android.com/topic/libraries/testing-support-library/release-notes.html
    //
    // https://maven.google.com/com/android/support/test/espresso/espresso-core/3.0.0/espresso-core-3.0.0.pom

    def runnerVersion = "1.6.1"
    def rulesVersion = "1.6.1"
    def espressoVersion = "3.6.1"
    def junitVersion = "4.13.2"

    // Update exclusions based on ./gradlew :app:androidDependencies
    implementation("androidx.test:runner:$runnerVersion") {
        exclude module: 'support-annotations'
    }

    implementation("androidx.test.espresso.idling:idling-concurrent:$espressoVersion") {
        exclude module: 'support-annotations'
    }
    implementation("androidx.test.espresso.idling:idling-net:$espressoVersion") {
        exclude module: 'support-annotations'
    }

    implementation Libs.MATERIAL_DESIGN
    implementation Libs.ANDROIDX_SWIPE_REFRESH_LAYOUT

    implementation Libs.GSON

    api "androidx.test.espresso:espresso-accessibility:$espressoVersion"

    /* OkHttp Idling Resource */
    implementation('com.jakewharton.espresso:okhttp3-idling-resource:1.0.0') {
        exclude module: 'espresso-idling-resource'
    }

    api("androidx.test:rules:$rulesVersion") {
        exclude module: 'support-annotations'
    }
    api("androidx.test.espresso:espresso-core:$espressoVersion") {
        exclude module: 'support-annotations'
    }

    // Fix IncompatibleClassChangeError https://code.google.com/p/android/issues/detail?id=183454
    api("androidx.test.espresso:espresso-contrib:$espressoVersion") {
        exclude module: 'design'
        exclude module: 'recyclerview-v7'
        exclude module: 'support-v4'
        exclude module: 'support-annotations'
        exclude module: 'support-core-utils'
        exclude module: 'support-compat'
        exclude module: "protobuf-lite"
    }
    implementation("androidx.test.espresso:espresso-idling-resource:$espressoVersion")
    api("androidx.test.espresso:espresso-intents:$espressoVersion") {
        exclude module: 'support-annotations'
    }
    api("androidx.test.espresso:espresso-web:$espressoVersion") {
        exclude module: 'support-annotations'
    }
    api("junit:junit:$junitVersion")
    implementation Libs.KOTLIN_STD_LIB

    api('com.adevinta.android:barista:4.2.0') {
        exclude group: 'com.android.support'
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'androidx.test.espresso'
        exclude group: 'androidx.test'
    }

    implementation project(':canvas-api-2')
    implementation 'com.github.javafaker:javafaker:0.18'
    /* Mock web server */
    implementation("com.squareup.okhttp3:mockwebserver:${Versions.OKHTTP}")

    /* DI */
    implementation Libs.HILT
    kapt Libs.HILT_COMPILER
    implementation Libs.HILT_TESTING
    kapt Libs.HILT_TESTING_COMPILER
    implementation Libs.HILT_ANDROIDX_WORK

    implementation Libs.COMPOSE_UI_TEST_MANIFEST
    implementation Libs.COMPOSE_UI_TEST
    implementation Libs.COMPOSE_UI

    implementation Libs.ANDROIDX_WORK_MANAGER
    implementation Libs.ANDROIDX_WORK_MANAGER_KTX
}


